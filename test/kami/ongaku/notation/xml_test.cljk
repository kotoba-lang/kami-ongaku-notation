(ns kami.ongaku.notation.xml-test
  (:require [clojure.test :refer [deftest is testing]]
            [kami.ongaku.notation.xml :as xml]))

(deftest escape-unescape-test
  (testing "text escaping round-trips"
    (is (= "a &amp; b &lt;c&gt;" (xml/escape-text "a & b <c>")))
    (is (= "a & b <c>" (xml/unescape-text "a &amp; b &lt;c&gt;"))))
  (testing "attr escaping also escapes quotes"
    (is (= "say &quot;hi&quot;" (xml/escape-attr "say \"hi\"")))))

(deftest parse-basic-test
  (testing "parses attributes, nested elements, self-closing tags, text"
    (let [doc (xml/parse "<?xml version=\"1.0\"?>\n<!-- a comment -->\n<root a=\"1\" b=\"two\"><child/><child>hi &amp; bye</child></root>")]
      (is (= "root" (:tag doc)))
      (is (= {"a" "1" "b" "two"} (:attrs doc)))
      (is (= 2 (count (:children doc))))
      (let [[c1 c2] (:children doc)]
        (is (= "child" (:tag c1)))
        (is (= [] (:children c1)))
        (is (= "child" (:tag c2)))
        (is (= "hi & bye" (xml/text-content c2)))))))

(deftest find-child-test
  (let [doc (xml/parse "<root><a>1</a><b>2</b><a>3</a></root>")]
    (is (= "1" (xml/text-content (xml/find-child doc "a"))))
    (is (= ["1" "3"] (mapv xml/text-content (xml/find-children doc "a"))))
    (is (nil? (xml/find-child doc "missing")))))

(deftest doctype-skip-test
  (testing "DOCTYPE without an internal subset is skipped"
    (let [doc (xml/parse "<!DOCTYPE root PUBLIC \"x\" \"y\"><root><a/></root>")]
      (is (= "root" (:tag doc))))))
